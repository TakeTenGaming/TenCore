buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}

	dependencies { classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT' }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = mod_version
group = "taketengaming.tencore"
archivesBaseName = "TenCore"

// Eclipse task compat
sourceCompatibility = targetCompatibility = java_compatibility
compileJava {
	sourceCompatibility = targetCompatibility = java_compatibility
}

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"
	makeObfSourceJar = true
	useDepAts = true
	mappings = mcp_mappings
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}
tasks.build.dependsOn('deobfJar')
tasks.build.dependsOn('generatePomFileForMavenJavaPublication')
tasks.build.dependsOn('publishToMavenLocal')

repositories {
	// Local Maven
	mavenLocal()

	// JustEnoughItems
	maven { url = "http://dvs1.progwml6.com/files/maven" }
}

dependencies {
	// DatMod
	deobfCompile "taketengaming.datmod:DatMod:" + mc_version + "-" + datmod_version
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}